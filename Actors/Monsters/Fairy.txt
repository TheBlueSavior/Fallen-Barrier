Class FB_Fairy : Actor
{
  Default
  {
    Health 20;
    Radius 16;
    Height 30;
    Mass 50;
    MeleeRange 100;
	MeleeThreshold 180;
    Speed 3;
    Scale 1;
	Painchance 128;
    SeeSound "";
    DeathSound "FB/FairyDeath";
    AttackSound "";
	PainSound "FB/FairyPain";
    Tag "Fairy";
    Monster;
    +DONTHURTSPECIES
    +NOGRAVITY
    +FLOAT
    +FLOATBOB
    +DONTFALL
    +NOICEDEATH
	+AVOIDMELEE
	FloatBobStrength 0.5;
	//MinMissileChance 240;
	Obituary "%o was swiftly destroyed by a possessed Fairy.";
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_SmallDemonicTeleportFog";
  }

  States
  {
  Spawn:
    FA_1 AB 5 A_Look();
    Loop;
  See:
    FA_1 AABB 1 A_Chase();
    Loop;
  WanderAttack:
	FA_1 AABBAABBAABBAABBAABB 1
	{
		A_Chase("Missile","Missile",CHF_DONTMOVE);
		A_Wander();
		A_FaceTarget();
	}
	FA_1 A 0 A_CheckSight("See");
	Loop;
  Realign:
	FA_1 AABBAABBAABBAABBAABBAABBAABBAABBAABBAABBAABBAABB 1 A_Wander();
	Goto See;
  Missile:
	TNT1 A 0 A_CheckLOF("MissileProceed");
    Goto Realign;
  MissileProceed:
  Bullet1:
	FA_1 A 3 A_SpawnProjectile("FB_FairyBall1",10);
	Goto CoolDown;
  CoolDown:
	FA_1 AABBAABBAABB 1 A_Wander();
	Goto WanderAttack;
  Melee:
    FA_1 AABB 1;
    Goto CoolDown;
  Pain:
	FA_1 A 1;
	FA_1 A 1 A_Pain();
	FA_1 A 4; 
	Goto See;
  Death:
    TNT1 A 0;
	FA_1 A 1 A_Scream();
	FA_1 A 1 A_NoBlocking();
  DeathFade:
	FA_1 A 1 A_FadeOut(0.1);
    Loop;
  }
}