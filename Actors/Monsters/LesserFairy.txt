Class FB_LesserFairy : FB_FairyBase
{
  bool seeskip;
  Default
  {
    Health 10;
    Radius 12;
    Height 15;
    Mass 30;
    MeleeRange 100;
	MeleeThreshold 180;
	MaxTargetRange 850;
    Speed 3;
    Scale 1;
	Alpha 0.9;
	DamageFunction 10;
	Painchance 256;
    SeeSound "FB/LesserFairySight";
    DeathSound "";
	PainSound "FB/FairyPain";
    Tag "Lesser Fairy";
    Monster;
    +DONTHARMCLASS
    +NOGRAVITY
    +FLOAT
    +FLOATBOB
    +DONTFALL
    +NOICEDEATH
	-AVOIDMELEE
	+THRUSPECIES
	FloatBobStrength 0.3;
	Obituary "%o was knocked down by a lucky Lesser Fairy.";
  }
    override void start() {
	//Push some possible sprites
	possSprite.push("LF_1");
	super.start();
	}
  States
  {
  Spawn:
	LF_1 A 0 NoDelay;
	"####" A 0 { if(seeskip) SetStateLabel("Spawn1");}
  Spawn1:
  Idle:
    "####" AB 5 A_Look();
    Loop;
  See:
	"####" A 0 { seeskip = true; }
	"####" A 0 ThrustThingZ(0,random(0,12),0,1);
	"####" ABABABABAB random(1,5);
	"####" A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
	Goto See1;
  See1:
    "####" AABB 1 A_Chase();
    Loop;
  CoolDown:
    "####" A 0;
	"####" AAABBBAAABBBAAABBBAAABBBAAABBB 1 A_Wander();
	Goto See1;
  Missile:
	"####" A 0 A_CheckLOF("MissileFire");
    Goto Realign;
  MissileFire:
	"####" A 0 A_JumpIfCloser(200,"RealignChoice");
  MissileTrueFire:
	"####" ABABABABABABABABABAB 1
		{
		A_FaceTarget();
		A_SpawnParticleEx
		(
		"",
		tex[1],
		style: STYLE_Add,
		flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL,
		lifetime: random(2,8),
		size: frandom(10,50),
		angle: 0,
		xoff: 10,
		yoff: 0,
		zoff: 6,
		startalphaf: 0.85,
		fadestepf: -1,
		sizestep: -15,
		startroll: random(-180,180)
		);
		}
	"####" A 3 A_SpawnProjectile("FB_LesserFairyBall1",6,angle:frandom(-4,4),flags:CMF_OFFSETPITCH,pitch:frandom(-4,4));
	Goto Cooldown;
  RealignChoice:
	"####" A 0 A_Jump(128,"MissileTrueFire");
  Realign:
	"####" AABBAABBAABBAABBAABBAABBAABBAABBAABBAABBAABBAABB 1 A_Wander();
	Goto See1;
  Pain:
	"####" A 1;
	"####" A 1 A_Pain();
	"####" A 4; 
	Goto See1;
  Death:
    "####" A 0;
	"####" A 0 { deathtimer = random(3,7); }
	"####" A 1 A_Scream();
	"####" A 1 A_NoBlocking();
  DeathEnd:
	TNT1 A 0
		{
		A_SpawnParticleEx
			(   
			"",
			TexMan.CheckForTexture("SPKWE0"),
			style: STYLE_Add,
			flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
			lifetime: random(200,300),
			size: 6,
			zoff: 5,
			accelx: frandom(-0.05,0.05),
			accely: frandom(-0.05,0.05),
			accelz: frandom(-0.05,0.2),
			startalphaf: frandom(0.8,1),
			fadestepf: 0,
			sizestep: -0.01,
			startroll: random(-180,180)
			);
		}
	TNT1 AAAA 0
		{
		A_SpawnParticleEx
			(
			"",
			TexMan.CheckForTexture("GNSMK0"),
			style: STYLE_Add,
			flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
			lifetime: random(40,50),
			size: frandom(6,10),
			velx: frandom(-0.5,0.5),
			vely: frandom(-0.5,0.5),
			velz: frandom(-0.5,0.5),
			startalphaf: frandom(0.8,0.95),
			fadestepf: -1,
			sizestep: frandom(0.1,1),
			startroll: random(-180,180),
			rollvel: frandom(-1,1)
			);
		}
	TNT1 AAAAAA 0
		{
		A_SpawnParticleEx
			(
			"",
			TexMan.CheckForTexture("DUS_C0"),
			style: STYLE_Normal,
			flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
			lifetime: random(10,30),
			size: frandom(4,10),
			velx: frandom(-2,2),
			vely: frandom(-2,2),
			velz: frandom(-2,3),
			accelz: frandom(-0.1,-0.6),
			startalphaf: 1.0,
			sizestep: -0.15,
			startroll: random(-180,180),
			rollvel: random(0,4)
			);
		}
	TNT1 A 0 A_StartSound("FB/DeathSFXMedium",14,0,0.5,ATTN_NORM,1.4);
	TNT1 A 0
		{
		A_SpawnParticleEx
		(
			"",
			TexMan.CheckForTexture("SPKWC0"),
			style: STYLE_Add,
			flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL,
			lifetime: 10,
			size: 200,
			angle: 0,
			xoff: 0,
			yoff: 0,
			zoff: 6,
			startalphaf: 1,
			fadestepf: -1,
			sizestep: -15,
			startroll: random(-180,180)
			);
		}
	TNT1 AAAAA 0
		{
		A_SpawnParticleEx
			(
			"",
			TexMan.CheckForTexture("Graphics/Particles/fbparticlesquare.png"),
			style: STYLE_Add,
			flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
			lifetime: random(20,60),
			size: frandom(2,12),
			velx: frandom(-4,4),
			vely: frandom(-4,4),
			velz: frandom(-4,5),
			accelz: frandom(-0.0,-0.8),
			startalphaf: 1.0,
			fadestepf: -0.5,
			sizestep: -0.2,
			startroll: random(-180,180)
			);
		}
    Stop;
  }
}